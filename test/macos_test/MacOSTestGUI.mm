#include <Carbon/Carbon.h>
#import <Cocoa/Cocoa.h>
#include <iostream>

#include "./MacOSTestGUI.h"

// è‡ªå®šä¹‰è§†å›¾ç±»ï¼Œç”¨äºå¤„ç†é”®ç›˜å’Œé¼ æ ‡äº‹ä»¶
@interface EventHandlingView : NSView {
    MacOSTestGUI* _guiInstance;
}
- (instancetype)initWithFrame:(NSRect)frameRect gui:(MacOSTestGUI*)gui;
@end

@implementation EventHandlingView

- (instancetype)initWithFrame:(NSRect)frameRect gui:(MacOSTestGUI*)gui
{
    self = [super initWithFrame:frameRect];
    if (self) {
        _guiInstance = gui;
    }
    return self;
}

- (BOOL)acceptsFirstResponder
{
    return YES;
}

- (void)keyDown:(NSEvent*)event
{
    _guiInstance->handleKeyEvent(
        MacOSTestGUI::KeyDown, [event keyCode], [event modifierFlags], [[event characters] UTF8String],
        [[event charactersIgnoringModifiers] UTF8String]);
}
- (void)keyUp:(NSEvent*)event
{
    _guiInstance->handleKeyEvent(
        MacOSTestGUI::KeyUp, [event keyCode], [event modifierFlags], [[event characters] UTF8String],
        [[event charactersIgnoringModifiers] UTF8String]);
}

- (void)mouseDown:(NSEvent*)event
{
    NSPoint location = [event locationInWindow];
    _guiInstance->handleMouseEvent(
        MacOSTestGUI::MouseDown, MacOSTestGUI::Left, location.x, location.y, (unsigned long long)[event modifierFlags]);
}
- (void)mouseDragged:(NSEvent*)event
{
    NSPoint location = [event locationInWindow];
    _guiInstance->handleMouseEvent(
        MacOSTestGUI::MouseMove, MacOSTestGUI::Left, location.x, location.y, (unsigned long long)[event modifierFlags]);
}
- (void)mouseUp:(NSEvent*)event
{
    NSPoint location = [event locationInWindow];
    _guiInstance->handleMouseEvent(
        MacOSTestGUI::MouseUp, MacOSTestGUI::Left, location.x, location.y, (unsigned long long)[event modifierFlags]);
}
- (void)rightMouseDown:(NSEvent*)event
{
    NSPoint location = [event locationInWindow];
    _guiInstance->handleMouseEvent(
        MacOSTestGUI::MouseDown, MacOSTestGUI::Right, location.x, location.y, (unsigned long long)[event modifierFlags]);
}
- (void)rightMouseDragged:(NSEvent*)event
{
    NSPoint location = [event locationInWindow];
    _guiInstance->handleMouseEvent(
        MacOSTestGUI::MouseMove, MacOSTestGUI::Right, location.x, location.y, (unsigned long long)[event modifierFlags]);
}
- (void)rightMouseUp:(NSEvent*)event
{
    NSPoint location = [event locationInWindow];
    _guiInstance->handleMouseEvent(
        MacOSTestGUI::MouseUp, MacOSTestGUI::Right, location.x, location.y, (unsigned long long)[event modifierFlags]);
}
- (void)otherMouseDown:(NSEvent*)event
{
    NSPoint location = [event locationInWindow];
    _guiInstance->handleMouseEvent(
        MacOSTestGUI::MouseDown, MacOSTestGUI::Other, location.x, location.y, (unsigned long long)[event modifierFlags]);
}
- (void)otherMouseDragged:(NSEvent*)event
{
    NSPoint location = [event locationInWindow];
    _guiInstance->handleMouseEvent(
        MacOSTestGUI::MouseMove, MacOSTestGUI::Other, location.x, location.y, (unsigned long long)[event modifierFlags]);
}
- (void)otherMouseUp:(NSEvent*)event
{
    NSPoint location = [event locationInWindow];
    _guiInstance->handleMouseEvent(
        MacOSTestGUI::MouseUp, MacOSTestGUI::Other, location.x, location.y, (unsigned long long)[event modifierFlags]);
}

@end

MacOSTestGUI::MacOSTestGUI(const std::string& windowTitle)
{
    // è·å–å½“å‰é”®ç›˜å¸ƒå±€
    keyboardLayout = TISCopyCurrentKeyboardLayoutInputSource();
    if (keyboardLayout) {
        CFDataRef layoutData = (CFDataRef)TISGetInputSourceProperty(keyboardLayout, kTISPropertyUnicodeKeyLayoutData);
        if (layoutData) {
            keyboardLayoutData = (const UCKeyboardLayout*)CFDataGetBytePtr(layoutData);
        }
    }

    NSApplication* app = [NSApplication sharedApplication];

    // è®¾ç½®åº”ç”¨ä¸ºæ™®é€šå‰å°åº”ç”¨ï¼Œå¦åˆ™æ— æ³•æ¥æ”¶é”®ç›˜äº‹ä»¶
    [app setActivationPolicy:NSApplicationActivationPolicyRegular];

    // åˆ›å»ºçª—å£
    NSRect frame = NSMakeRect(100, 100, 400, 400);
    NSWindow* window = [[NSWindow alloc] initWithContentRect:frame
                                                   styleMask:(NSWindowStyleMaskTitled | NSWindowStyleMaskClosable
                                                              | NSWindowStyleMaskMiniaturizable | NSWindowStyleMaskResizable)
                                                     backing:NSBackingStoreBuffered
                                                       defer:NO];

    [window setTitle:[NSString stringWithUTF8String:windowTitle.c_str()]];

    // è®¾ç½®çª—å£ä¸ºé¡¶å±‚çª—å£
    [window setLevel:NSFloatingWindowLevel];

    // åˆ›å»ºè‡ªå®šä¹‰å®¹å™¨è§†å›¾ï¼ˆæ”¯æŒäº‹ä»¶å¤„ç†ï¼‰
    EventHandlingView* containerView = [[EventHandlingView alloc] initWithFrame:NSMakeRect(0, 0, 400, 400) gui:this];

    // åˆ›å»ºæ–‡æœ¬æ ‡ç­¾ï¼ˆä¸Šé¢ï¼‰
    NSTextField* textField = [[NSTextField alloc] initWithFrame:NSMakeRect(0, 320, 400, 60)];
    [textField setStringValue:@"è¿™æ˜¯ä¸€ä¸ªæµ‹è¯•çª—å£ï¼Œç”¨äº MaaFW è¯†åˆ«ã€‚"];
    [textField setEditable:NO];
    [textField setBordered:NO];
    [textField setBackgroundColor:[NSColor clearColor]];
    [textField setAlignment:NSTextAlignmentCenter];
    [textField setFont:[NSFont systemFontOfSize:14]];
    [containerView addSubview:textField];

    // åˆ›å»º emoji æ ‡ç­¾ï¼ˆä¸‹é¢å±…ä¸­ï¼‰
    NSTextField* emojiField = [[NSTextField alloc] initWithFrame:NSMakeRect(150, 150, 100, 100)];
    [emojiField setStringValue:@"ğŸ"];
    [emojiField setEditable:NO];
    [emojiField setBordered:NO];
    [emojiField setBackgroundColor:[NSColor clearColor]];
    [emojiField setAlignment:NSTextAlignmentCenter];
    [emojiField setFont:[NSFont systemFontOfSize:100]];
    [containerView addSubview:emojiField];

    [window setContentView:containerView];

    // æ˜¾ç¤ºçª—å£
    [window makeKeyAndOrderFront:nil];
}

MacOSTestGUI::~MacOSTestGUI()
{
    // é‡Šæ”¾é”®ç›˜å¸ƒå±€èµ„æº
    if (keyboardLayout) {
        CFRelease(keyboardLayout);
        keyboardLayout = nullptr;
        keyboardLayoutData = nullptr;
    }
}

void MacOSTestGUI::run()
{
    NSApplication* app = [NSApplication sharedApplication];
    [app run];
}

void MacOSTestGUI::handleKeyEvent(
    KbdEventType type, unsigned short keyCode, uint64_t modifiers, std::string characters, std::string charactersIgnoringModifiers)
{
    std::cout << std::endl;

    // æ‰“å°æŒ‰é”®
    std::cout << "Key: ";
    if (modifiers & NSEventModifierFlagCommand) {
        std::cout << "âŒ˜ ";
    }
    if (modifiers & NSEventModifierFlagShift) {
        std::cout << "â‡§ ";
    }
    if (modifiers & NSEventModifierFlagControl) {
        std::cout << "âŒƒ ";
    }
    if (modifiers & NSEventModifierFlagOption) {
        std::cout << "âŒ¥ ";
    }
    if (modifiers & NSEventModifierFlagCapsLock) {
        std::cout << "â‡ª ";
    }
    // ä½¿ç”¨ç¼“å­˜çš„é”®ç›˜å¸ƒå±€è·å–æŒ‰é”®åç§°
    std::cout << keyCodeToString(keyCode) << std::endl;

    std::cout << "KeyCode: " << keyCode << std::endl;

    std::cout << "Characters: '" << characters << "'" << std::endl;
}

void MacOSTestGUI::handleMouseEvent(MouseEventType type, MouseButton button, double x, double y, unsigned long long modifiers)
{
    std::cout << "é¼ æ ‡";
    switch (type) {
    case MouseDown:
        std::cout << "æŒ‰ä¸‹";
        break;
    case MouseUp:
        std::cout << "é‡Šæ”¾";
        break;
    case MouseMove:
        std::cout << "ç§»åŠ¨";
        break;
    }

    std::cout << " - ";
    switch (button) {
    case Left:
        std::cout << "å·¦é”®";
        break;
    case Right:
        std::cout << "å³é”®";
        break;
    case Other:
        std::cout << "å…¶ä»–";
        break;
    }

    std::cout << " åæ ‡: (" << x << ", " << y << ")" << std::endl;
}

std::string MacOSTestGUI::keyCodeToString(unsigned short keyCode)
{
    if (!keyboardLayoutData) {
        return "";
    }

    // å°è¯•è·å–æŒ‰é”®åç§°
    UniChar unicodeString[4];
    UniCharCount actualStringLength = 0;
    UInt32 deadKeyState = 0;

    // ä½¿ç”¨UCKeyTranslateè·å–æŒ‰é”®å¯¹åº”çš„å­—ç¬¦
    OSStatus status = UCKeyTranslate(
        keyboardLayoutData, keyCode, kUCKeyActionDisplay,
        0, // æ— ä¿®é¥°ç¬¦
        LMGetKbdType(), kUCKeyTranslateNoDeadKeysBit, &deadKeyState, sizeof(unicodeString) / sizeof(unicodeString[0]), &actualStringLength,
        unicodeString);

    if (status == noErr && actualStringLength > 0) {
        return std::string([NSString stringWithCharacters:unicodeString length:actualStringLength].UTF8String);
    }

    return "";
}
